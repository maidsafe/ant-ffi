plugins {
    alias libs.plugins.androidLibrary
    alias libs.plugins.jetbrainsKotlinAndroid
    alias libs.plugins.ktfmt
    // The magic plugin that automates the cargo integration
    alias libs.plugins.cargo.ndk
}

android {
    namespace = 'com.maidsafe.autonomi.core'
    compileSdk = 36
    ndkVersion = "26.2.11394342"

    defaultConfig {
        minSdk = 29
        targetSdk = 36

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation libs.androidx.ktx
    implementation libs.androidx.appcompat

    // Essential! Note that UniFFI dictates the minimum supported version, and this may change with new releases
    // TODO: Migrate version to TOML (doesn't work). Likely related issue: https://github.com/gradle/gradle/issues/21267
    //noinspection UseTomlInstead
    implementation 'net.java.dev.jna:jna:5.15.0@aar'

    // For JVM unit tests, we need the full JNA JAR (not AAR) which includes native libraries
    //noinspection UseTomlInstead
    testImplementation 'net.java.dev.jna:jna:5.15.0'

    testImplementation libs.junit

    androidTestImplementation libs.androidx.test.junit
    androidTestImplementation libs.androidx.test.espresso
}

cargoNdk {
    module  = "../rust"  // Directory containing Cargo.toml
    librariesNames = ["libant_ffi.so"]
    extraCargoBuildArguments = ["-p", "ant-ffi"]
}

// Build Rust library for host platform (for JVM unit tests)
tasks.register('buildCargoHost', Exec) {
    workingDir '../../rust'
    commandLine 'cargo', 'build', '-p', 'ant-ffi'

    doLast {
        def hostLibDir = layout.buildDirectory.dir("rustJniLibs/desktop").get().asFile
        hostLibDir.mkdirs()

        // Copy the built library to the test resources
        def sourceLib = file("../../rust/target/debug/libant_ffi.so")
        def destLib = new File(hostLibDir, "libant_ffi.so")

        if (sourceLib.exists()) {
            copy {
                from sourceLib
                into hostLibDir
            }
        }
    }
}

// Generate UniFFI bindings for unit tests
tasks.register('generateTestUniFFIBindings', Exec) {
    workingDir '../../rust'
    def bDir = layout.buildDirectory.dir("generated/source/uniffi/test/java").get()
    commandLine 'cargo', 'run', '-p', 'uniffi-bindgen', 'generate', '--library', '../rust/target/debug/libant_ffi.so', '--language', 'kotlin', '--out-dir', bDir

    dependsOn 'buildCargoHost'

    doFirst {
        bDir.asFile.mkdirs()
    }
}

// Ensure host library is built before running tests
tasks.withType(Test).configureEach {
    dependsOn 'buildCargoHost', 'generateTestUniFFIBindings'

    def libDir = layout.buildDirectory.dir("rustJniLibs/desktop").get().asFile.absolutePath

    // Add the host library directory to both java.library.path and jna.library.path
    systemProperty 'java.library.path', libDir
    systemProperty 'jna.library.path', libDir
}

// Add generated bindings to test source set
android.sourceSets {
    test.java.srcDirs += layout.buildDirectory.dir("generated/source/uniffi/test/java")
}

android.libraryVariants.all { variant ->
    def bDir = layout.buildDirectory.dir("generated/source/uniffi/${variant.name}/java").get()
    def generateBindings = tasks.register("generate${variant.name.capitalize()}UniFFIBindings", Exec) {
        workingDir '../../rust'
        commandLine 'cargo', 'run', '-p', 'uniffi-bindgen', 'generate', '--library', '../android/core/src/main/jniLibs/arm64-v8a/libant_ffi.so', '--language', 'kotlin', '--out-dir', bDir

        dependsOn "buildCargoNdk${variant.name.capitalize()}"
    }

    variant.javaCompileProvider.get().dependsOn(generateBindings)

    // Some stuff here is broken, since Android Tests don't run after running gradle build,
    // but do otherwise. Also CI is funky.
    tasks.named("compile${variant.name.capitalize()}Kotlin").configure {
        dependsOn generateBindings
    }

    tasks.named("connectedDebugAndroidTest").configure {
        dependsOn generateBindings
    }

    def sourceSet = variant.sourceSets.find { it.name == variant.name }
    sourceSet.java.srcDir layout.buildDirectory.file("generated/source/uniffi/${variant.name}/java")

    // UniFFI tutorial notes that they made several attempts like this but were unsuccessful coming
    // to a good solution for forcing the directory to be marked as generated (short of checking in
    // project files, I suppose).
//    idea.module.generatedSourceDirs += file("${buildDir}/generated/source/uniffi/${variant.name}/java/uniffi")
}